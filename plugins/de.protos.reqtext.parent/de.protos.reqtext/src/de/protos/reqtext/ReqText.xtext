grammar de.protos.reqtext.ReqText with org.eclipse.xtext.common.Terminals

generate reqText "http://www.protos.de/reqtext/ReqText"

RModel:
	imports += RImport*
	(
		states 	+= State 		|
		classes += Class 		|
		objs 	+= SpecObject
	)*
;

SpecObject:
	'SpecObject' name=ID '{'
		'description' description=CC_STRING
		'state' state = [State]
		'classification' class = [Class]
		'version' version = Version
		('image' image = Image)?
		('reference' reference = [SpecObject])?
		children += SpecObject*		
	'}' 
;

Image:
	name = CC_STRING url = CC_STRING 
;

State:
	'State' name=ID
;

Class:
	'Classification' name=ID
;

Version:
	major=INT '.' minor=INT
;

RImport:
	'import' importURI=STRING
;

terminal CC_STRING:
	"'''" -> "'''";
